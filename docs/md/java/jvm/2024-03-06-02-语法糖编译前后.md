---
title: 第二章、语法糖编译前后
---
# 第二章、语法糖编译前后

## 2.1 语法糖概述

语法糖是指在编程语言中提供的一种语法特性，它并不改变语言的功能或语义，但可以使代码更加简洁易读，提高开发效率。这些语法糖通常是编译器或解释器在编译或解释源代码时自动转换成标准的语法结构

截止到 JDK8，Java语言中的语法糖一共有 16 种：
1. 自动装箱/拆箱（基本类型）
2. 泛型
3. 内部类
4. 枚举
5. switch支持枚举字符串
7. 字符串拼接
8. 数值字面量
9. 可变参数
10. foreach 循环
12. Lambda 表达式
13. assert
15. try-with-resources
16. 在同一 catch 代码块中捕获

就一一来看，每一种语法糖在源码中是什么样的，编译成字节码后又是什么样的

## 2.2 自动装箱拆箱

Java的基本数据类型都有其对应的包装类，装箱就是将基本数据类型转换为包装类，拆箱就是将包装类转化为基本数据类型。而自动装箱拆箱就是隐式的转化，在JDK1.5引入：

```java
public class Main {
    public static void main(String[] args) {
        // 自动装箱、拆箱
        Integer a1 = 1;
        int a2 = a1;
        // 通过类型转换的装箱、拆箱
        Integer b1 = (Integer) 1;
        int b2 = (int) b1;
        // 通过包装类的装箱、拆箱
        Integer c1 = Integer.valueOf(1);
        int c2 = c1.intValue();
    }
}
```

我们来看这个方法的字节码指令：

```text
 0 iconst_1
 1 invokestatic #2 <java/lang/Integer.valueOf : (I)Ljava/lang/Integer;>
 4 astore_1
 5 aload_1
 6 invokevirtual #3 <java/lang/Integer.intValue : ()I>
 9 istore_2
10 iconst_1
11 invokestatic #2 <java/lang/Integer.valueOf : (I)Ljava/lang/Integer;>
14 astore_3
15 aload_3
16 invokevirtual #3 <java/lang/Integer.intValue : ()I>
19 istore 4
21 iconst_1
22 invokestatic #2 <java/lang/Integer.valueOf : (I)Ljava/lang/Integer;>
25 astore 5
27 aload 5
29 invokevirtual #3 <java/lang/Integer.intValue : ()I>
32 istore 6
34 return
```

发现3种写法，最终的实现是一样的：装箱使用的是包装类的valueOf方法，而拆箱使用的是包装类的xxxValue方法(这里是Integer就是intValue方法)

## 2.3 泛型

泛型即如`<T>`，使用于一些通用的工具类，比较著名的如`ArrayList`、`HashMap`等等。首先看这段代码：

```java
public class Main<T> {
    T obj;
    public static void main(String[] args) {
        Main<Integer> main = new Main<>();
        main.obj = 2;
        int i = main.obj;
    }
}
```

这里将泛型指定为Integer，并做了简单的赋值操作；而在字节码层面，会发现obj字段是Object类型：

```text
 0 new #2 <Main>
 3 dup
 4 invokespecial #3 <Main.<init> : ()V>
 7 astore_1
 8 aload_1
 9 iconst_2
10 invokestatic #4 <java/lang/Integer.valueOf : (I)Ljava/lang/Integer;>
13 putfield #5 <Main.obj : Ljava/lang/Object;>
16 aload_1
17 getfield #5 <Main.obj : Ljava/lang/Object;>
20 checkcast #6 <java/lang/Integer>
23 invokevirtual #7 <java/lang/Integer.intValue : ()I>
26 istore_2
27 return
```

行号17可以明显看出，obj是Object类型，而使用时强转成了我们指定的Integer。<font color="orange">编译器在做泛型擦除时，会以泛型限制中能表达的最高层类来代替泛型，这里因为没有做任何限制，所以就是Object</font>。那么我们给泛型做出如下限制：

```java
public class Main<T extends Number> {
    T obj;
    public static void main(String[] args) {
        Main<Integer> main = new Main<>();
        main.obj = 2;
        int i = main.obj;
    }
}
```

是否obj字段就是Number类了呢：

```text
 0 new #2 <Main>
 3 dup
 4 invokespecial #3 <Main.<init> : ()V>
 7 astore_1
 8 aload_1
 9 iconst_2
10 invokestatic #4 <java/lang/Integer.valueOf : (I)Ljava/lang/Integer;>
13 putfield #5 <Main.obj : Ljava/lang/Number;>
16 aload_1
17 getfield #5 <Main.obj : Ljava/lang/Number;>
20 checkcast #6 <java/lang/Integer>
23 invokevirtual #7 <java/lang/Integer.intValue : ()I>
26 istore_2
27 return
```

## 2.4 内部类

内部类，因为会使类结构变复杂难懂，所以使用的比较少。在类内部定义类、方法内定义类等等都统称为内部类。内部类在编译后会生成额外的class文件：

```java
public class Main {
    static class FieldInner {}
    public static void main(String[] args) {
        FieldInner student = new FieldInner();
        class MethodInner{}
        MethodInner methodInner = new MethodInner();
    }
}
```

```text
D:\work\JStudy\design\target\classes>dir
 驱动器 D 中的卷是 data
 卷的序列号是 64DB-5D67
 D:\work\JStudy\design\target\classes 的目录
2024/03/07  10:46    <DIR>          .
2024/03/07  10:46    <DIR>          ..
2024/03/07  10:46               383 Main$1MethodInner.class
2024/03/07  10:46               313 Main$FieldInner.class
2024/03/07  10:46               571 Main.class
3 个文件          1,267 字节
3 个目录 48,527,339,520 可用字节
```

## 2.5 枚举

Java中的枚举是一种特殊的数据类型，用于定义一组命名的常量。枚举类型的优点包括类型安全、易读性和易维护性，因为枚举明确了允许的取值范围，并且编译器可以帮助检查代码中是否使用了无效的取值

```java
public enum MyEnum {
    TEST("test"),PRE("pre"),PROD("prod");
    private String env;

    MyEnum(String env) {
        this.env = env;
    }

    public String getEnv() {
        return env;
    }

    public void setEnv(String env) {
        this.env = env;
    }
}
```

该枚举中提供了3个环境枚举，并定义了一个env对象(当然这个对象其实也可以不用，因为每个枚举实例本身就可以用来区分环境，这里是为了能看到更多字节码内容)。直接看该类的字节码：

```text
// 发现枚举类在编译后，也是一个class，且是final的
public final class MyEnum extends java.lang.Enum<MyEnum>
```

```text
// 访问标记上看，和类描述中的一致，public final enum
flags: ACC_PUBLIC, ACC_FINAL, ACC_SUPER, ACC_ENUM
```

```text
// 定义的3个枚举实例，其实都是MyEnum的实例对象，且是static、final的，而枚举内定义的变量则和普通类中一致
public static final MyEnum TEST;
    descriptor: LMyEnum;
    flags: ACC_PUBLIC, ACC_STATIC, ACC_FINAL, ACC_ENUM

  public static final MyEnum PRE;
    descriptor: LMyEnum;
    flags: ACC_PUBLIC, ACC_STATIC, ACC_FINAL, ACC_ENUM

  public static final MyEnum PROD;
    descriptor: LMyEnum;
    flags: ACC_PUBLIC, ACC_STATIC, ACC_FINAL, ACC_ENUM
```

此外，枚举类还默认有一个数组`$VALUES`，访问标志为`private static final synthetic`，用来保存所有枚举实例

```text
// 枚举中自带的方法，获取所有枚举类型，其实就是将$VALUES变量用clone方法拷贝，并返回
public static MyEnum[] values();
descriptor: ()[LMyEnum;
flags: ACC_PUBLIC, ACC_STATIC
Code:
stack=1, locals=0, args_size=0
0: getstatic     #1                  // Field $VALUES:[LMyEnum;
3: invokevirtual #2                  // Method "[LMyEnum;".clone:()Ljava/lang/Object;
6: checkcast     #3                  // class "[LMyEnum;"
9: areturn
LineNumberTable:
line 1: 0

// 跟据变量名，获取枚举，通过调用Enum类中的valueOf方法实现
public static MyEnum valueOf(java.lang.String);
descriptor: (Ljava/lang/String;)LMyEnum;
flags: ACC_PUBLIC, ACC_STATIC
Code:
stack=2, locals=1, args_size=1
0: ldc           #4                  // class MyEnum
2: aload_0
3: invokestatic  #5                  // Method java/lang/Enum.valueOf:(Ljava/lang/Class;Ljava/lang/String;)Ljava/lang/Enum;
6: checkcast     #4                  // class MyEnum
9: areturn
LineNumberTable:
line 1: 0
LocalVariableTable:
Start  Length  Slot  Name   Signature
0      10     0  name   Ljava/lang/String;
```

还有枚举中最重要的一部分，static代码块，首先看一下枚举实例创建的过程：

```text
// 这里有两个信息，第一是除了我们自定义的变量值test之外，还有两个字段，分别是枚举实例名TEST和枚举实例的顺序0；第二是枚举实例也是通过枚举类的构造器创建的
0: new           #4                  // class MyEnum
3: dup
4: ldc           #8                  // String TEST
6: iconst_0
7: ldc           #9                  // String test
9: invokespecial #10                 // Method "<init>":(Ljava/lang/String;ILjava/lang/String;)V
12: putstatic     #11                 // Field TEST:LMyEnum;
```

以及还有`$VALUES`的构建过程：

```text
45: iconst_3
46: anewarray     #4                  // class MyEnum
49: dup
50: iconst_0
51: getstatic     #11                 // Field TEST:LMyEnum;
54: aastore
55: dup
56: iconst_1
57: getstatic     #14                 // Field PRE:LMyEnum;
60: aastore
61: dup
62: iconst_2
63: getstatic     #17                 // Field PROD:LMyEnum;
66: aastore
67: putstatic     #1                  // Field $VALUES:[LMyEnum;
70: return
```

::: tip 枚举总结

1. 枚举实际上是java.lang.Enum类的子类，且都是final的说明枚举类不能被继承
2. 每个枚举实例其实都是枚举类的对象实例，且为static final，因此只能在static代码块中进行初始化
3. 每个枚举实例，除了自定义的变量外，还有两个变量：枚举实例名以及枚举实例定义顺序
4. 需要注意的时，枚举的构造器是private，也就是说无法在运行期间创建枚举实例，只能通过在枚举中定义实例来创建

:::

## 2.6 switch支持枚举和String

```java
public class Main {
    public static void main(String[] args) {
        MyEnum prod = MyEnum.PROD;
        switch (prod){
            case PRE:
            case PROD:
            case TEST:
            default:
        }
    }
}
```

```text
 0 getstatic #2 <MyEnum.PROD : LMyEnum;>
 3 astore_1
 4 getstatic #3 <Main$1.$SwitchMap$MyEnum : [I>
 7 aload_1
 8 invokevirtual #4 <MyEnum.ordinal : ()I>
11 iaload
12 tableswitch 1 to 3 //switch跳转，case值连续
	1:  40 (+28)
	2:  40 (+28)
	3:  40 (+28)
	default:  40 (+28)
40 return
```

switch支持枚举，是将枚举转化为枚举的ordinal来做的

```java
public class Main {
    public static void main(String[] args) {
        switchTest("abc");
    }

    public static void switchTest(String str) {
        switch (str) {
            case "abc": {
                System.out.println("abc");
                break;
            }
            case "edf": {
                System.out.println("def");
                break;
            }
            default: {
                System.out.println("hij");
                break;
            }
        }
    }
}
```

```text
  0 aload_0 //局部变量槽，第一个变量即Str压入栈顶
  1 astore_1 //栈顶出栈并保存到第二个局部变量槽
  2 iconst_m1 //栈顶入常量-1
  3 istore_2 //栈顶出栈并保存到第三个局部变量槽
  4 aload_1 //第二局部变量槽入栈
  5 invokevirtual #4 <java/lang/String.hashCode : ()I> //获取Str的hash值
  8 lookupswitch 2 // switch跳转，case不连续时使用，2表示长度
	96354:  36 (+28)
	100263:  50 (+42)
	default:  61 (+53)
 36 aload_1 // 第二局部变量槽入栈
 37 ldc #5 <abc> // 常量abc入栈
 39 invokevirtual #6 <java/lang/String.equals : (Ljava/lang/Object;)Z> //执行equals方法
 42 ifeq 61 (+19) // 返回值等于0，也就是不相等时，跳转61行
 45 iconst_0 
 46 istore_2 //相等将常量0放入第三局部变量槽
 47 goto 61 (+14)
 50 aload_1 
 51 ldc #2 <edf>
 53 invokevirtual #6 <java/lang/String.equals : (Ljava/lang/Object;)Z>
 56 ifeq 61 (+5)
 59 iconst_1
 60 istore_2 // 这里类似，如果相等将常量1放入第三局部变量槽
 61 iload_2
 62 lookupswitch 2 //将上面的匹配结果，在这里做跳转，如果是-1则走default
	0:  88 (+26)
	1:  99 (+37)
	default:  110 (+48)
 88 getstatic #7 <java/lang/System.out : Ljava/io/PrintStream;>
 91 ldc #5 <abc>
 93 invokevirtual #8 <java/io/PrintStream.println : (Ljava/lang/String;)V>
 96 goto 118 (+22)
 99 getstatic #7 <java/lang/System.out : Ljava/io/PrintStream;>
102 ldc #9 <def>
104 invokevirtual #8 <java/io/PrintStream.println : (Ljava/lang/String;)V>
107 goto 118 (+11)
110 getstatic #7 <java/lang/System.out : Ljava/io/PrintStream;>
113 ldc #10 <hij>
115 invokevirtual #8 <java/io/PrintStream.println : (Ljava/lang/String;)V>
118 return
```

通过阅读字节码，可以总结出switch支持String的规则：

1. 首先通过hash匹配到匹配项
2. 再执行equals方法
3. 如果equals方法结果为true，则执行该项内容；否则执行default内的逻辑

这里因为执行hashCode方法和equals方法，也导致如果str字段为null，会抛出NPE异常

## 2.7 字符串拼接

```java
public class Main {
    public static void main(String[] args) {
        concat("123456");
    }

    private static String concat(String s){
        return s + "abc" + "def";
    }
}
```

给出concat方法的字节码：

```text
 0 new #4 <java/lang/StringBuilder>
 3 dup
 4 invokespecial #5 <java/lang/StringBuilder.<init> : ()V>
 7 aload_0
 8 invokevirtual #6 <java/lang/StringBuilder.append : (Ljava/lang/String;)Ljava/lang/StringBuilder;>
11 ldc #7 <abcdef>
13 invokevirtual #6 <java/lang/StringBuilder.append : (Ljava/lang/String;)Ljava/lang/StringBuilder;>
16 invokevirtual #8 <java/lang/StringBuilder.toString : ()Ljava/lang/String;>
19 areturn
```

发现，字符串拼接操作是通过StringBuilder来完成的，而如果是两个常量字符串的拼接，如代码中的abc+def，会直接合并成一个常量字符串

## 2.8 数值字面量

```java
public class Main {
    public static void main(String[] args) {
        int i = 1000_000_000;
    }
}
```

```text
0 ldc #2 <1000000000>
2 istore_1
3 return
```

JDK7开始，为了数值常量可读性，支持数值添加下划线，而在字节码中则是不存在下划线的

## 2.9 可变参数

```java
public class Main {
    public static void main(String[] args) {
       arrayParam("s", "t", "r", "i", "n", "g");
    }

    private static void arrayParam(String ... text){
        String s = text[0];
    }
}
```

```text
0 getstatic #10 <java/lang/System.out : Ljava/io/PrintStream;>
3 aload_0 //引入第一个局部变量槽的对象
4 iconst_2 //引入常量2
5 aaload //获取下标为2的数组中元素
6 invokevirtual #11 <java/io/PrintStream.println : (Ljava/lang/String;)V>
9 return
```

从字节码中可以看出，可变参数其实是转化成了数组，而在编码中其语法也和数组一模一样

## 2.10 foreach循环

```java
public class Main {
    public static void main(String[] args) {
        int[] ints = {9, 8, 7};
        test(ints);
    }

    private static void test(int[] ints){
        for (int i : ints) {
            System.out.println(i);
        }
    }
}
```

```text
 0 aload_0
 1 astore_1
 2 aload_1
 3 arraylength
 4 istore_2
 5 iconst_0
 6 istore_3
 7 iload_3
 8 iload_2
 9 if_icmpge 31 (+22)
12 aload_1
13 iload_3
14 iaload
15 istore 4
17 getstatic #3 <java/lang/System.out : Ljava/io/PrintStream;>
20 iload 4
22 invokevirtual #4 <java/io/PrintStream.println : (I)V>
25 iinc 3 by 1
28 goto 7 (-21)
31 return
```

可以看到还是通过下标循环访问数组来做的，而如ArrayList这类容器则是通过迭代器实现的



